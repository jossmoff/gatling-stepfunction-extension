plugins {
    id 'scala'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.20.0'
    id 'maven-publish'
    id 'signing'
}

group = 'dev.joss'
version = '1.3.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


repositories {
    mavenCentral()
}

ext {
    scalaVersion = "2.13.11"
    gatlingVersion = "3.9.5"
    awsSdkVersion = "2.20.119"
}


dependencies {
    implementation("org.scala-lang:scala-library:${scalaVersion}")
    implementation("io.gatling:gatling-core:${gatlingVersion}")
    implementation("io.gatling:gatling-commons:${gatlingVersion}")
    implementation("software.amazon.awssdk:sfn:${awsSdkVersion}")
}


spotless {
    scala {
        scalafmt()
    }
}


java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.joss'
            artifactId = 'gatling-stepfunction-extension'
            from components.java
            pom {
                name = 'Gatling Step Function Extension'
                description = 'A Gatling extension for executing step functions.'
                url = 'https://github.com/jossmoff/gatling-stepfunction-extension'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'jossmoff'
                        name = 'Joss Moffatt'
                        email = 'josshmoffatt@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:jossmoff/gatling-stepfunction-extension.gitt'
                    developerConnection = 'scm:git@github.com:jossmoff/gatling-stepfunction-extension.git'
                    url = 'https://github.com/jossmoff/gatling-stepfunction-extension'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("sonatypeUsername")
                password = project.findProperty("sonatypePassword")
            }
        }
    }
}

signing {
    def signingKey = project.findProperty("signingKey")
    def signingPassword = project.findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}